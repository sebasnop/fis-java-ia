FUNCTION_BLOCK tipper	// Definición de bloque

// El único tipo de variable soportada es REAL

// Definición de las variables de entrada
VAR_INPUT				
        humedad : REAL; RANGE := (0 .. 10);
        intensidad : REAL; RANGE := (0 .. 10);
        volumen : REAL; RANGE := (0 .. 10);
END_VAR

// Definición de las variables de salida
VAR_OUTPUT				
        temperatura : REAL; RANGE := (165 .. 225);
END_VAR

// Fuzzificación de la variable de entrada 'humedad'
FUZZIFY humedad
        TERM baja := trape 0 0 0.5 4.5;
        TERM media := trian 0.5 5 9.5;
        TERM alta := trape 5.5 9.5 10 10;
END_FUZZIFY

// Fuzzificación de la variable de entrada 'intensidad'
FUZZIFY intensidad
        TERM baja := trape 0 0 3 8;
        TERM alta := trape 4 9 10 10;
END_FUZZIFY

// Fuzzificación de la variable de entrada 'volumen'
FUZZIFY volumen
        TERM pequeno := trape 0 0 1 4;
        TERM mediano := trian 1 5 9;
        TERM grande := trape 6 9 10 10;
END_FUZZIFY

// Defuzzificación de la variable de salida 'temperatura'
DEFUZZIFY temperatura
        TERM baja := trape 165 165 175 185;
        TERM media := trian 180 195 210;
        TERM alta := trape 205 215 225 225;

        METHOD : COG;		// Usa el metodo de 'Centro de Gravedad'
        DEFAULT := 0;		// Valor por defecto si ninguna regla activa el defuzzificador
END_DEFUZZIFY

// Reglas de inferencia
RULEBLOCK No1
        AND : MIN;	// Usa el 'mín' para el 'AND' y por defecto el 'máx' para el 'OR' (operador difuso).
        ACT : MIN;	// Usa el metodo 'min' para la activación/implicación (trunca)
        ACCU : MAX;	// Usa el 'max' para la acumulación/agregación

        RULE 1 : IF (intensidad IS baja) AND (humedad IS baja) THEN temperatura IS baja;
        RULE 2 : IF (intensidad IS baja) AND (humedad IS media) AND ((volumen IS pequeno) OR (volumen IS mediano)) THEN temperatura IS media;
        RULE 3 : IF (intensidad IS baja) AND (humedad IS media) AND (volumen IS grande) THEN temperatura IS alta;
        RULE 4 : IF (intensidad IS baja) AND (humedad IS alta) AND (volumen IS pequeno) THEN temperatura IS media;
        RULE 5 : IF (intensidad IS baja) AND (humedad IS alta) AND ((volumen IS mediano) OR (volumen IS grande)) THEN temperatura IS alta;
        RULE 6 : IF (intensidad IS alta) AND (humedad IS baja) THEN temperatura IS baja;
        RULE 7 : IF (intensidad IS alta) AND (humedad IS media) AND ((volumen IS pequeno) OR (volumen IS mediano)) THEN temperatura IS baja;
        RULE 8 : IF (intensidad IS alta) AND (humedad IS media) AND (volumen IS grande) THEN temperatura IS media;
        RULE 9 : IF (intensidad IS alta) AND (humedad IS alta) THEN temperatura IS media;
END_RULEBLOCK

END_FUNCTION_BLOCK